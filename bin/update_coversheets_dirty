#!/usr/bin/perl -w 

use FindBin;
use lib "$FindBin::Bin/../../../perl_lib";

use strict;
use EPrints;

use Getopt::Long;

my $unset_errors = 0;
my $force = 0;
my $quiet = 0;
my $verbose = 0;
my $help = 0;

GetOptions(
		'unset-errors' => \$unset_errors,
		'force' => \$force,
        'quiet' => \$quiet,
        'verbose' => \$verbose,
        'help|?' => \$help,
) || usage() ;

if( $help )
{
        usage();
}

if( scalar( @ARGV ) != 1 )
{
        usage();
}

my $session = new EPrints::Session( 1, $ARGV[0] ) or die("cant create Session object");

my $update_dirty_ts_file = $session->config( "variables_path" ) . "/coversheets_update_dirty.timestamp";
my $hashes_ts_file = $session->config( "variables_path" ) . "/coversheets_hashes.timestamp";
my $update_dirty_ts = -f $update_dirty_ts_file ? EPrints::Utils::mtime( $update_dirty_ts_file ) : 0;
my $hashes_ts = -f $hashes_ts_file ? EPrints::Utils::mtime( $hashes_ts_file ) : 0;
if ( $hashes_ts < $update_dirty_ts && ! $force )
{
    print "\nNOTICE: Coversheet front and back file hashes have not been changed since the last time update_coversheets_dirty ran.  To run anyway add --force option.\n\n" unless $quiet;
    exit;
}

my $cs_ds = $session->dataset( 'coversheet' );
my $cs_search_exp = $cs_ds->prepare_search();
$cs_search_exp->add_field(
	fields => [ $cs_ds->field( 'status' ) ],
	value => 'active',
	match => 'EX',
);

my $coversheets = $cs_search_exp->perform_search;
my $counter = 0;

$coversheets->map(sub {
	my ( $session, $dataset, $coversheet ) = @_;

	print "Checking eprints needing coversheets_dirty set for coversheet #".$coversheet->id."\n" if $verbose;
	my $cs_ff_hash = $coversheet->get_value( 'frontfile_hash' ) || '';
	my $cs_bf_hash = $coversheet->get_value( 'backfile_hash' ) || '';

	my $doc_ds = $session->dataset( 'document' );
	my $search_exp = $doc_ds->prepare_search();
	$search_exp->add_field(
	        fields => [ $doc_ds->field( 'coversheetid' ) ],
	        value => $coversheet->id,
        	match => "EX",
	);

	my $documents = $search_exp->perform_search;
	$documents->map(sub{
		my( $session, $dataset, $document ) = @_;

		if ( $document->get_value( 'coversheet_error' ) && ( $document->get_value( 'coversheet_error' ) eq "1" || $document->get_value( 'coversheet_error' ) eq "TRUE" ) )
		{
			if ( $unset_errors )
			{
				print "Unsetting coversheet_error for document #".$document->id." for eprint #".$document->get_value( 'eprintid' )."\n" unless $quiet;
				$document->set_value( "coversheet_error", undef );
				$document->commit;
			}
			else
			{
				return;
			}
		}
		my $eprint = $document->get_eprint;
		return if $eprint->get_value( 'coversheets_dirty' ) eq "TRUE";
		my $doc_ff_hash = $document->get_value( 'coversheet_frontfile_hash' ) || '';
		my $doc_bf_hash = $document->get_value( 'coversheet_backfile_hash' ) || '';
		return if $cs_ff_hash eq $doc_ff_hash && $cs_bf_hash eq $doc_bf_hash;
		print "Setting coversheets_dirty to TRUE for eprint #".$eprint->id."\n" unless $quiet;
		$eprint->set_value( 'coversheets_dirty', 'TRUE' );
		$eprint->commit;
		$counter++;
	});
});

open(my $fh_ts, ">:raw", $update_dirty_ts_file) or EPrints::abort( "Can't write to $update_dirty_ts_file: $!" );
print $fh_ts EPrints::Time::human_time();
close($fh_ts);

print "\n$counter eprints have had their coversheets_dirty flag set to TRUE\n\n" unless $quiet;

sub usage
{
        print <<USAGE;

$0 <ARCHIVE_ID>

\t--unset-errors   Unsets coversheet_error on documents
\t--force          Run even if coversheet front/back file hashes have not changed
\t--quiet          Enable quiet mode
\t--verbose        Enable verbose mode
\t--help           Show this help

USAGE

        exit( 1 );
}

1;
